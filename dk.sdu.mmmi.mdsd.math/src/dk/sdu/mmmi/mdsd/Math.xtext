grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Program:
	{Program} 'program' name=ID (ext+=External)* math=MathExp
;

External returns Expression:
	{External} 'external' name=ID '(' (params+=ID (',' params+=ID)*)? ')'
;

ExternalUse returns Expression:
	{ExternalUse} ref=[External] '(' (params+=Exp (',' params+=Exp)*)? ')'
;

MathExp:
	{MathExp} variables += VarBinding*
;

VarBinding returns Binding:
	{VarBinding} 'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | Parenthesis  |  VariableUse | LetBinding | ExternalUse
;

Parenthesis returns Expression:
	{Parenthesis} '(' exp=Exp ')'
;

LetBinding returns Binding:
	{LetBinding} 'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding returns Expression:
	VarBinding | LetBinding
;

VariableUse returns Expression:
	{VariableUse} ref = [Binding]
;
